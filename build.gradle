buildscript {
  repositories {
    jcenter()
    maven { url 'http://dl.bintray.com/kaleidos/maven' }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:1.5.0'
    classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.9'
    classpath 'net.kaleidos.gradle.plugins:emerger:0.0.2'
  }
}

repositories {
  jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'net.kaleidos.gradle.plugins.emerger'
apply from: "$rootDir/gradle/extraAndroidTasks.gradle"

android {
  compileSdkVersion 23
  buildToolsVersion '23.0.2'

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 23

    versionCode 1
    versionName '1.0.0'

    signingConfig signingConfigs.debug

    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }

  signingConfigs {
    debug {
      storeFile file('debug.keystore')
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.dev'

      minifyEnabled false
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    disable 'InvalidPackage' // lint sees references in the grooid jar to unavailable java classes
  }
}

androidGroovy {
  options {
    configure(groovyOptions) {
      javaAnnotationProcessing = true
    }
  }
  skipJavaC = true
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.4.5:grooid'

  compile 'com.android.support:support-v4:23.2.0'
  compile 'com.android.support:appcompat-v7:23.2.0'
  compile 'com.android.support:recyclerview-v7:23.2.0'
  compile 'com.android.support:design:23.2.0'
  compile 'com.android.support:support-annotations:23.2.0'

  compile 'com.squareup.dagger:dagger:1.2.2'
  provided 'com.squareup.dagger:dagger-compiler:1.2.2'

  compile 'io.reactivex:rxjava:1.1.0'
  compile 'io.reactivex:rxandroid:1.1.0'

  compile 'com.squareup.okhttp3:okhttp:3.1.1'
  compile 'com.squareup.okhttp3:logging-interceptor:3.1.1'
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.1'
  compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
  compile 'com.squareup.retrofit2:converter-moshi:2.0.0-beta4'
  compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'

  compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.0'

  compile 'com.jakewharton.timber:timber:4.0.1'
  compile 'com.jakewharton.byteunits:byteunits:0.9.1'
  compile 'com.jakewharton.threetenabp:threetenabp:1.0.2'

  compile 'com.arasthel:swissknife:1.4.0'

  debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
  releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

  androidTestCompile 'com.andrewreitz:spock-android:1.2.1'
  androidTestCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  androidTestCompile 'com.android.support.test:runner:0.4.1'
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'

  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

configurations.all {
  resolutionStrategy.force 'com.android.support:support-annotations:23.0.1'
  resolutionStrategy.force 'org.codehaus.groovy:groovy:2.4.5:grooid'
  resolutionStrategy.force 'junit:junit:4.12'
}
